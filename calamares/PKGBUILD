# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# https://gitlab.manjaro.org/manjaro-arm/packages/extra/calamares/-/blob/master/PKGBUILD
# https://gitlab.manjaro.org/packages/extra/calamares/-/blob/master/PKGBUILD
# https://gitlab.manjaro.org/packages/extra/calamares-git/-/blob/master/PKGBUILD

pkgname=commonarch-calamares
_pkgname=calamares
pkgver=3.3.7.240514
_pkgver=3.3.7
_replaceversion=3.3.6
pkgrel=01
_commit=48554cf
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://github.com/calamares/calamares/"
license=('LGPL')
depends=(
	'boost-libs'
	'cryptsetup'
	'doxygen'
	'efibootmgr'
	'gptfdisk'
	'gtk-update-icon-cache'
	'hwinfo'
	'icu'
	'kconfig'
	'kcoreaddons'
	'kcrash'
	'ki18n'
	'kparts'
	'kpmcore'
	'dosfstools'
	'kservice'
	'kwidgetsaddons'
	'libpwquality'
	'polkit-qt6'
	'rsync'
	'qt6-declarative'
	'solid'
	'squashfs-tools'
	'yaml-cpp'
	'kvantum'
)
makedepends=(
	'boost'
	'cmake'
	'extra-cmake-modules'
	'git'
	'ninja'
	'python-jsonschema'
	'python-pyaml'
	'python-unidecode'
	'qt6-tools'
)
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source=($_pkgname::git+https://github.com/calamares/calamares#commit=$_commit
	"calamares.desktop"
	"calamares_polkit"
	"49-nopasswd-calamares.rules")

sha256sums=('1b698e07e9a2e9a9c83d713a824a80e9fa7b9cdbe33f8d093051214aa4667f57'
            'ff6be5cf9924208ec98ec824ecc4a8f08a6b54c1f8f9b82d66d166f703b04b5b'
            '26954f96967be0b57a9a1bace99dd3f7e33fa6ea8a70e21ada4e809c9e464b5c'
            '56d85ff6bf860b9559b8c9f997ad9b1002f3fccc782073760eca505e3bddd176')

options=(!debug)

prepare() {
	cd ${srcdir}/calamares
	sed -i -e 's/etc\/sddm.conf/etc\/sddm.conf.d\/kde_settings.conf/' src/modules/displaymanager/main.py

	# change version
	_ver="$(cat CMakeLists.txt | grep -m3 -e "  VERSION" | grep -o "[[:digit:]]*" | xargs | sed s'/ /./g')"
	sed -i -e "s|CALAMARES_VERSION $_replaceversion|CALAMARES_VERSION $_pkgver|g" CMakeLists.txt

}

build() {
	cd $_pkgname

  cmake -S . -Bbuild \
      -GNinja \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DWITH_APPSTREAM=OFF \
      -DWITH_PYBIND11=OFF \
      -DWITH_QT6=ON \
      -DSKIP_MODULES="dracut \
          dracutlukscfg \
          dummycpp \
          dummyprocess \
          dummypython \
          dummypythonqt \
          initramfs \
          initramfscfg \
          interactiveterminal \
          keyboardq \
          license \
          localeq \
          oemid \
          packagechooserq \
          partitionq \
          services-openrc \
          summaryq \
          tracking \
          usersq \
          welcomeq \
          zfs \
          zfshostid"

  cmake --build build
}
package() {
	cd ${srcdir}/calamares/build
	DESTDIR="${pkgdir}" cmake --build . --target install
	install -Dm644 "${srcdir}/calamares.desktop" "$pkgdir/etc/xdg/autostart/calamares.desktop"
    install -Dm644 "${srcdir}/calamares.desktop" "$pkgdir/usr/share/applications/calamares.desktop"
	install -Dm755 "${srcdir}/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
	install -Dm644 "${srcdir}/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
	chmod 750 "$pkgdir"/etc/polkit-1/rules.d
}